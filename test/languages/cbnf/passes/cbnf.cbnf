// Copyright (c) 2017-2019 Cranium Software

pass "cbnf" switch
{
    output language.name + ".cbnf" ;
    
    write "// CBNF grammar definition for " + language.name + "\n\n" ;

    write "language \"" + language.name + "\" ;\n\n" ;
    
    for each value in language.line-comments
    {
        write "comment \"" + value + "\" ;\n" ;
    }
    for each value in language.block-comments
    {
        write "comment " ;
        for each part in value
        {
            write "\"" + part + "\" " ;
        }
        write ";\n";
    }
    write "\n" ;
    
    for each value in language.lexemes
    {
        if value.is-optional
        {
        }
        else
        {
            write "lexeme " + value.name + " " + value + " ;\n" ;
        }
    }
    write "\n" ;
    
    for each value in language.quotes
    {
        write "quote " + value.name + " " ;
        for each part in value
        {
            write "\"" + part + "\" " ;
        }
        write ";\n" ;
    }
    // handled by the spacing in the productions.
    //write "\n" ;
    
    last = "" ;
    
    for each production in language.productions
    {
        if production.input-name == last
        {
        }
        else
        {
            write "\n" ;
        }
        
        last = production.input-name ;
        
        write production.input-name + " = " ;
        join = "" ;
        
        for each production-name in production.names
        {
            write join ;
            write production-name.input-name ;
            if production-name.is-list
            {
                if production-name.is-non-empty
                {
                    write "+" ;
                }
                else
                {
                    write "*" ;
                }
            }
            else
            {
                if production-name.is-optional
                {
                    write "?" ;
                }
            }
            join = " " ;
        }
        
        write " ;\n" ;
    }
    
    write "\n" ;
    
    write "keywords\n" ;
    write "{\n" ;
    for each value in language.keywords
    {
        write "\t" + value + "\n";
    }
    write "}\n\n";
    
    write "identifiers\n" ;
    write "{\n" ;
    for each value in language.identifiers
    {
        write "\t" + value + "\n";
    }
    write "}\n\n";
    
    write "operators\n" ;
    write "{\n" ;
    for each value in language.operators
    {
        write "\t" + value + "\n";
    }
    write "}\n\n";
    
    write "separators\n" ;
    write "{\n" ;
    for each value in language.separators
    {
        write "\t" + value + "\n" ;
    }
    write "}\n\n";
    
    write "terminators\n" ;
    write "{\n" ;
    for each value in language.terminators
    {
        write "\t" + value + "\n" ;
    }
    write "}\n\n";
    
    write "strings\n" ;
    write "{\n" ;
    for each value in language.strings
    {
        write "\t" + value + "\n" ;
    }
    write "}\n" ;
    
    // output passes?
    for each compiler-pass in language.passes
    {
        write "\npass " + "\"" + compiler-pass.name + "\"\n" ;
        
        tab-prefix = "" ;
        current-prefix = "" ;
        walk compiler-pass.parse as node
        {
            if node.is-list
            {
            }
            else
            {
                if node == "."
                {
                    write "." ;
                }
                else
                {
                    if node == ";"
                    {
                        write node + "\n" ;
                        current-prefix = tab-prefix + "" ; // + "" as hack to reduce parse complexity
                    }
                    else
                    {
                        if node == "{"
                        {
                            write "\n" ;
                            current-prefix = tab-prefix + "" ; // + "" as hack to reduce parse complexity
                            tab-prefix = tab-prefix + "    " ;
                        }
                        if node == "}"
                        {
                            tab-prefix = tab-prefix - "    " ;
                            current-prefix = tab-prefix + "" ; // + "" as hack to reduce parse complexity
                        }
                        write current-prefix + node + " " ;
                        current-prefix = "" ;
                        if node == "{"
                        {
                            write "\n" ;
                            current-prefix = tab-prefix  + "" ; // + "" as hack to reduce parse complexity
                        }
                        if node == "}"
                        {
                            write "\n";
                            current-prefix = tab-prefix + "" ; // + "" as hack to reduce parse complexity
                        }
                        
                    }
                }
            }
        }
    }
}
